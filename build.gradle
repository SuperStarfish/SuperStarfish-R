buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
        classpath 'org.slf4j:slf4j-simple:1.7.6'
    }
}

plugins {
    id 'net.saliman.cobertura' version '2.2.8'
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    // SuperStarfishMania application version
    version = '9.0'

    ext {
        appName = 'standup'
        gdxVersion = '1.5.6'
        roboVMVersion = '1.0.0'
        ashleyVersion = '1.3.1'
        aiVersion = '1.5.0'

        // log
        slf4jVersion = '1.7.6'

        // testing & analysis tools
        junitVersion = '4.11'
        mockitoVersion = '2.0.6-beta'

        androidSupportRepository = '19'
        androidSupportLibrary = '23.0.1'
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    apply from: "${rootProject.rootDir}/config/checkstyle.gradle"
    apply from: "${rootProject.rootDir}/config/findbugs.gradle"
    apply from: "${rootProject.rootDir}/config/pmd.gradle"

    if (!"${project}".contains("android")) {
        apply from: "${rootProject.rootDir}/config/cobertura.gradle"
    }


}

project(":android") {
    apply plugin: "android"

    configurations { natives }

    dependencies {
        compile project(":launcher")

        // sqldroid for sqlite
        compile "org.sqldroid:sqldroid:1.0.3"

        // the libgdx platform
        compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"

        // the freetype libraries
        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"

        // Add dependency for AccelLibAndroid.
        compile fileTree(dir: 'libs', include: 'accel-lib-android.jar')

        // android support library
        // used for android notifications
        compile "com.android.support:support-v4:$androidSupportLibrary"
    }
}

project(":desktop") {
    apply plugin: "java"

    dependencies {
        compile project(":launcher")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"

    }
}

project(":launcher") {
    apply plugin: "java"

    dependencies {
        compile project(":view")
    }
}

project(":aquarium") {
    apply plugin: "java"

    dependencies {
        compile project(":view")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"

    }
}

project(":view") {
    apply plugin: "java"

    dependencies {
        compile project(":game_logic")

        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
    }
}

project(":game_logic") {
    apply plugin: "java"

    dependencies {
        compile project(":client")
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "org.slf4j:slf4j-simple:$slf4jVersion"
        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"

        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
        testCompile "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testCompile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":client") {
    apply plugin: "java"

    dependencies {
        compile project(":server")

        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
    }
}

project(":server") {
    apply plugin: "java"

    dependencies {
        compile project(":data_structures")
        compile "org.xerial:sqlite-jdbc:3.8.6"
        compile "org.sqldroid:sqldroid:1.0.3"
        compile "org.slf4j:slf4j-simple:$slf4jVersion"

        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
    }
}

project(":data_structures") {
    apply plugin: "java"

    dependencies {
        compile "org.slf4j:slf4j-simple:$slf4jVersion"

        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
    }
}

tasks.eclipse.doLast {
    delete ".project"
}

test.finalizedBy { project.tasks.cobertura }
